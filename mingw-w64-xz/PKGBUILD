# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=xz
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.6.1
pkgrel=2
pkgdesc="Library and command line tools for XZ and LZMA compressed files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://tukaani.org/xz"
msys2_repository_url="https://github.com/tukaani-project/xz/"
msys2_references=(
  'archlinux: xz'
  "cpe: cpe:/a:tukaani:xz"
)
# TODO: check situation with getopt_long mentioned in COPYING for possible
# removal of LGPL from this field
license=('spdx:0BSD AND LGPL-2.1-or-later AND GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-gettext-runtime")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-gettext-tools"
             "${MINGW_PACKAGE_PREFIX}-doxygen"
             "po4a")
# WARNING: don't use the upstream tarball until this is resolved:
# https://github.com/tukaani-project/xz/issues/92
# https://nvd.nist.gov/vuln/detail/CVE-2024-3094
source=("https://github.com/tukaani-project/xz/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('237284fae40e5f8e9908f0a977e7d0b9a5c7c1c10a41b8e6ed0fb40e930467c8')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>

prepare() {
  cd "${_realname}-${pkgver}"

  ./autogen.sh
}


build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --disable-rpath \
    --disable-lzma-links

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING* -t ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}
}
