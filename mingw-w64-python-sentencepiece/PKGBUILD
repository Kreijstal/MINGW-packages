# Maintainer: kreijstal <kreijstal@hotmail.com>
__realname=sentencepiece
_realname=python-${__realname}
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.2.0
pkgrel=1
pkgdesc="Unsupervised text tokenizer for Neural Network-based text generation (MinGW-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/google/sentencepiece'
license=('Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-sentencepiece")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build" wheel "${MINGW_PACKAGE_PREFIX}-python-installer" 
"${MINGW_PACKAGE_PREFIX}-python-setuptools"
"${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("https://github.com/google/sentencepiece/archive/v${pkgver}.tar.gz"
        "0001-make-it-more-friendly-for-mingw-enviroments.patch")
sha256sums=('SKIP' 'SKIP')

prepare() {
  cd "${srcdir}/${__realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-make-it-more-friendly-for-mingw-enviroments.patch"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}"
  cp ${__realname}-${pkgver}/LICENSE ${__realname}-${pkgver}/python/LICENSE 

  cp -r "${__realname}-${pkgver}/python" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  SENTENCEPIECE_MINGW_SKIP=true ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}
package() {
  msg "Python install for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${__realname}/LICENSE"
}

