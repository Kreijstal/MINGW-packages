# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=pesq
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.0.4
pkgrel=2
pkgdesc='PESQ (Perceptual Evaluation of Speech Quality) Wrapper for Python Users (narrow band and wide band) (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/ludlows/python-pesq"
msys2_references=(
  'pypi: pesq'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-numpy")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-pytest-runner"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cython0")
options=('!emptydirs')
source=("https://pypi.org/packages/source/${_realname:0:1}/${_realname}/${_realname//-/_}-${pkgver}.tar.gz")
sha256sums=('b724b28f73fb638522982bd68e8c3c0957e2f45210639a460233b17aa7fc890b')

prepare() {
  cd "$srcdir"
  rm -rf python-build-${MSYSTEM} | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"
}

build() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
