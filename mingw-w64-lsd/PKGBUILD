# Maintainer: Peltoche <dev@halium.fr>
# Co-maintainer: Andrea Pascal <andrea@anodium.net>
# Co-Maintainer: Deeraurty at github

_realname=lsd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.2
pkgrel=2
pkgdesc="The next gen ls command (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/lsd-rs/lsd"
msys2_references=(
  'purl: pkg:cargo/lsd'
)
license=("spdx:Apache-2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust" "${MINGW_PACKAGE_PREFIX}-pkgconf")
options=('!strip' '!lto')
depends=("${MINGW_PACKAGE_PREFIX}-libgit2")
optdepends=("${MINGW_PACKAGE_PREFIX}-ttf-font-nerd: popular collections such as Font Awesome & fonts such as Hack")
           #'powerline-fonts: powerline symbols for terminals')
source=("https://github.com/lsd-rs/lsd/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "https://github.com/lsd-rs/lsd/pull/1021.patch")
sha256sums=('cd80dae9a8f6c4c2061f79084468ea6e04c372e932e3712a165119417960e14e'
            '8af2e1dd0a1cf1a55118f1c93d0c7ad5312562faa2ba06af73d2ebd0e32795e7')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/lsd-rs/lsd/pull/1021
  patch -Np1 -i "${srcdir}"/1021.patch

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

_env() {
  export LIBZ_SYS_STATIC=0
  export LIBGIT2_NO_VENDOR=1
  export WINAPI_NO_BUNDLED_LIBRARIES=1
  export SHELL_COMPLETIONS_DIR="$PWD/completions"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo build --release --frozen
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Unset lower-cased envs temp and tmp
  # that points to AppData/Local/Temp
  # to pass
  #   - test_list_almost_all_empty_directory
  #   - test_list_all_empty_directory
  unset temp
  unset tmp

  _env
  cargo test --release --frozen
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  _env
  cargo install \
    --offline \
    --no-track \
    --frozen \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  install -Dm644 completions/_lsd "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_lsd"
  install -Dm644 completions/lsd.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/lsd"
  install -Dm644 completions/lsd.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/lsd.fish"
}
