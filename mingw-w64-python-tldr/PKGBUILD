# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=tldr
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=3.2.0
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/tldr-pages/tldr-python-client'
msys2_references=(
  'archlinux: tldr'
  'pypi: tldr'
)
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python-colorama"
         "${MINGW_PACKAGE_PREFIX}-python-shtab"
         "${MINGW_PACKAGE_PREFIX}-python-termcolor")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-wheel"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx-argparse")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
options=('!strip')
source=("${_realname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('9e8f89411f297c2591bccdc3b1515f6b41662ce3a1514c072282d6f7979fc0dc')

build() {
  cd "${srcdir}"
  cp -r "tldr-python-client-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  # man page
  make -C docs
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m pytest
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE.md "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.md"

  # Generate shell completions
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/"
  python -m tldr --print-completion bash > "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/tldr"
  python -m tldr --print-completion zsh > "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_tldr"
}
