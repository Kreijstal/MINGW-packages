# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=typer
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.10.0
pkgrel=1
pkgdesc='Build great CLIs. Easy to code. Based on Python type hints (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://typer.tiangolo.com/'
msys2_repository_url='https://github.com/tiangolo/typer'
msys2_references=(
  'pypi: typer'
)
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-click"
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-flit-core"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
	     "${MINGW_PACKAGE_PREFIX}-python-wheel")
source=("${msys2_repository_url}/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
        '0001-typer-0.10.0-allow-newlest-deps.patch')
sha256sums=('08c745a0d356831071e544fd61fee8fbdeaa8ba9e5e2d6888275ff7f092ca61f'
            '53f723b7ba00bb2efc5178ccc98fe7bd0fccf3c2ce0b5f73be3c43ac21cca45d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/0001-typer-0.10.0-allow-newlest-deps.patch
}

build() {
  msg "Python build for ${MSYSTEM}"

  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/python-build-${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    "${MINGW_PREFIX}"/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
